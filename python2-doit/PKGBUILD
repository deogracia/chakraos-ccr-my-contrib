# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=python2-doit
_pyname=doit
pkgver=0.29.0
pkgrel=1
pkgdesc='doit automation tool'
arch=('any')
url='http://pydoit.org/'
license=('MIT')
options=(!emptydirs)
changelog=${pkgname}.changelog
source=("https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
depends=('python2' 'python2-pyinotify' 'python2-six' 'python2-setuptools' 'python2-cloudpickle')
sha256sums=('fcb479482644de3e83d6faed9b29373f1f38942b79b991a7432943a84416d5cd')

package() {
  cd "${srcdir}/${_pyname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
  ln -s ${_pyname} "${pkgdir}/usr/bin/${_pyname}3"
  install -D -m755 'zsh_completion_doit' "${pkgdir}/usr/share/zsh/site-functions/_${_pyname}"
  install -D -m755 'bash_completion_doit' "${pkgdir}/usr/share/bash-completion/completions/${_pyname}"
}

# vim:set ts=2 sw=2 et:
