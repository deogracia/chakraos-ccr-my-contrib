# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python2-tox'
pkgver=2.4.1
pkgrel=1
pkgdesc='Python virtualenv management and testing tool'
arch=('any')
url='http://testrun.org/tox/latest/'
license=('GPL2')
makedepends=('python2-setuptools' 'python2-py' 
             'python2-virtualenv' 'python2-pluggy')
checkdepends=('python2-pytest-runner')
depends=('python2-py' 'python2-virtualenv' 'python2-setuptools' 'python2-pluggy')
changelog=$pkgname.changelog
source=("https://pypi.io/packages/source/t/tox/tox-${pkgver}.tar.gz")
sha256sums=('6673571a3c4b1561b564e2b1cce55bddfb4f3efdb387ba4eb0a3688bbe2496db')

build() {
  cd "$srcdir"/tox-$pkgver
  python2 setup.py build
}

check() {
  # Hack entry points

  (
    cd "$srcdir"/tox-$pkgver
    virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
    . "$srcdir/pyvenv-py2/bin/activate"
    python2 setup.py install
    python2 setup.py ptr
  )
}

package() {

  cd "$srcdir"/tox-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/tox{,2}
  mv "$pkgdir"/usr/bin/{tox-quickstart,tox2-quickstart}
}
