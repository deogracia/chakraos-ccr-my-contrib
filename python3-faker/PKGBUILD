# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python3-faker'
_distname='faker'
pkgver=0.7.3
pkgrel=1
pkgdesc="Faker is a Python package that generates fake data for you. "
arch=('any')
url="http://faker.rtfd.org/"
license=('MIT')
depends=('python3-dateutil' 'python3-six')
makedepends=('git' 'python3-setuptools')
checkdepends=('python3-mock' 'python3-ukpostcodeparser' 'python3-email-validator')
changelog=${pkgname}.changelog
source=("https://github.com/joke2k/${_distname}/archive/v${pkgver}.tar.gz")
sha256sums=('e53a8c62a894f69a31d9d713364c240ea6eb1707dbb1641de0061f510ff66e86')
_PYTHON_CMD="python3"

build() {
  cd "${srcdir}/${_distname}-${pkgver}"
  ${_PYTHON_CMD} setup.py build
}

check() {
  cd "${srcdir}/${_distname}-${pkgver}"
  #${_PYTHON_CMD} setup.py test
  ${_PYTHON_CMD} -m unittest -v faker.tests
}

package() {
  cd "${srcdir}/${_distname}-${pkgver}"

  ${_PYTHON_CMD} setup.py install --root="$pkgdir/" --optimize=1

  # No physical licence file in this repo; I use the one created by me @ build time
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
