# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=ruby-shadow
pkgver=2.5.0
pkgrel=1
pkgdesc="Shadow password module"
arch=('i686' 'x86_64')
url="https://github.com/apalmblad/ruby-shadow"
license=('CPL')
depends=('ruby')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/apalmblad/$pkgname/archive/$pkgver.tar.gz)
sha1sums=('ac3e474975f1f0d03300f5dbe63d31ea2702ee5b')

build() {
  cd $pkgname-$pkgver

  ruby extconf.rb
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir"/ sitedir='$(rubylibprefix)/vendor_ruby' \
    sitehdrdir='$(rubyhdrdir)/vendor_ruby' install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
