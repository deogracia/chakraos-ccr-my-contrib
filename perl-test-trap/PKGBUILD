# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=perl-test-trap
_distname="Test-Trap"
pkgver=0.3.3
pkgrel=1
pkgdesc='Trap exit codes, exceptions, output, etc.'
arch=('any')
url="https://metacpan.org/release/Test-Trap"
license=('PerlArtistic' 'GPL')
depends=('perl-data-dump' 'perl-test-tester')
options=('!emptydirs')
changelog=${pkgname}.changelog
source=("http://search.cpan.org/CPAN/authors/id/E/EB/EBHANSSEN/Test-Trap-v${pkgver}.tar.gz")
sha256sums=('ac54b82027815892faabce21e094b852dd208b1f056cecfd78f183e9257ee698')

build() {
  cd ${_distname}-v${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_distname}-v${pkgver}
  make test
}

package() {
  cd ${_distname}-v${pkgver}
  make DESTDIR="${pkgdir}" install
}
