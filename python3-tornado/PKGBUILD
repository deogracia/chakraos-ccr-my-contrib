# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python3-tornado'
pkgver=4.5.1
pkgrel=1
pkgdesc='open source version of the scalable, non-blocking web server and tools'
arch=('i686' 'x86_64')
url='http://www.tornadoweb.org/'
license=('Apache')
depends=('python3')
optdepends=('python3-pycurl: for tornado.curl_httpclient'
            'python3-twisted: for tornado.platform.twisted')
makedepends=('python3-setuptools')
checkdepends=('python3-pycurl' 'python3-mock' 'python3-twisted')
changelog=$pkgname.changelog
source=("$pkgbase-$pkgver.tar.gz::https://github.com/tornadoweb/tornado/archive/v$pkgver.tar.gz"
        0001-use_system_ca_certificates.patch)
sha512sums=('ffc60d79432d2ef5f2f474ff2e023b926aec090c60318e9f745e19fddc274bca6c7c1e1a71460cb2aaf200d3010b802b85872d34f19058b7d97905c1896b6a56'
            'a6422735bdce26246088d38aec55042627a1800329847aba54ca85453dcefcdde631519b57088dd441a42a4c341e7f07c73ab6b73d8404869b67ee4107bde912')

prepare() {
  cd tornado-$pkgver
  patch -p1 -i ../0001-use_system_ca_certificates.patch

  find . -type f -name '*.py' -exec sed -i 's|#!/usr/bin/env python$|#!/usr/bin/env python3|' {} \;

  export TORNADO_EXTENSION=1
}

build() {
  cd tornado-$pkgver
  python3 setup.py build
}

check() {
  _PYV=`python3 -c "import sys;t='{v[0]}.{v[1]}'.format(v=list(sys.version_info[:2]));sys.stdout.write(t)";`
#  (
    cd tornado-$pkgver
    python3 setup.py install --root="$PWD/tmp_install" --optimize=1
    export PYTHONPATH="$PWD/tmp_install/usr/lib/python${_PYV}/site-packages:$PYTHONPATH"
    cd tmp_install
    python3 -m tornado.test.runtests
    python3 -m tornado.test.runtests --ioloop=tornado.platform.select.SelectIOLoop
    python3 -m tornado.test.runtests --httpclient=tornado.curl_httpclient.CurlAsyncHTTPClient
    python3 -m tornado.test.runtests --ioloop_time_monotonic
    python3 -m tornado.test.runtests --ioloop=tornado.platform.twisted.TwistedIOLoop
    python3 -m tornado.test.runtests --ioloop=tornado.platform.asyncio.AsyncIOLoop
    python3 -m tornado.test.runtests --resolver=tornado.netutil.ThreadedResolver
#  ) || warning "Tests failed"
}

package() {
  cd tornado-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
