# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=('python2-virtualenv')
pkgver=15.0.2
pkgrel=1
pkgdesc="Virtual Python Environment builder"
url="https://virtualenv.pypa.io/"
arch=('any')
license=('MIT')
makedepends=('python2' 'python2-sphinx')
depends=('python2')
replaces=('virtualenv')
conflicts=('virtualenv')
changelog=$pkgname.changelog
source=("https://pypi.python.org/packages/5c/79/5dae7494b9f5ed061cff9a8ab8d6e1f02db352f3facf907d9eb614fb80e9/virtualenv-$pkgver.tar.gz#md5=0ed59863994daf1292827ffdbba80a63")
sha256sums=('fab40f32d9ad298fba04a260f3073505a16d52539a84843cf8c8369d4fd17167')

package() {

  cd "$srcdir/virtualenv-$pkgver"

  # should report this upstream as still not fixed...
  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" virtualenv.py

  python2 setup.py build
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # Generate man page
  pushd "$srcdir/virtualenv-$pkgver/docs"
  make man

  install -Dm644 "_build/man/virtualenv.1" "${pkgdir}/usr/share/man/man1/virtualenv2.1"
  popd

  # move this "old" version out of the way
  mv "$pkgdir/usr/bin/virtualenv" "$pkgdir/usr/bin/virtualenv2"

  install -D -m644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
