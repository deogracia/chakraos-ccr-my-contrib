# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=facter
pkgver=3.6.6
pkgrel=1
pkgdesc="Collect and display system facts"
arch=('i686' 'x86_64')
url="http://puppetlabs.com/facter"
license=('APACHE')
depends=('ruby' 'yaml-cpp' 'boost-libs' 'curl')
makedepends=('boost' 'cmake' 'leatherman' 'cpp-hocon')
optdepends=('java-environment: jruby support')
checkdepends=('ruby-bundler')
replaces=('cfacter')
changelog=$pkgname.changelog
source=(http://downloads.puppetlabs.com/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('717f817128c3c4af8a5800276e6373865ad8ba836bfefb2af6d01daf494ce4e1aa79ee493ecc0f8912945238029d04515aacefcc523ea1632c1f5f140b565a78')

prepare() {
  cd $pkgname-$pkgver
  # Replace rb_data_object_alloc symbol with rb_data_object_wrap
  # https://tickets.puppetlabs.com/browse/FACT-1291
  sed -i 's/rb_data_object_alloc/rb_data_object_wrap/g' \
    $( grep -rl rb_data_object_alloc lib/src/ruby )
}

build() {
  cd $pkgname-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr

  make
}

check () {
  cd $pkgname-$pkgver
  pushd lib
  bundle install --path vendor/bundle
  popd
  make test ARGS=-V
}

package() {
  cd $pkgname-$pkgver

  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
