# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=siege
pkgver=4.0.4
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="An http regression testing and benchmarking utility"
url="https://www.joedog.org/siege-home/"
depends=('openssl')
changelog=$pkgname.changelog
source=("http://download.joedog.org/siege/siege-${pkgver}.tar.gz")
sha256sums=('8f7dcf18bd722bb9cc92bc3ea4b59836b4a2f8d8f01d4a94c8d181f56d91ea6f')
backup=(etc/siegerc etc/urls.txt)

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  install -d -m755 "$pkgdir/etc"
  install -d -m755 "$pkgdir/usr/share"
  make DESTDIR="$pkgdir" install
  sed -i 's/^# file =$/file = \/etc\/urls.txt/' "$pkgdir/etc/siegerc"
}

