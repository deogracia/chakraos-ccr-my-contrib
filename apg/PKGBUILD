# Maintainer: Lionel Félicité <deogracia@free.fr>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: eric <eric@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=apg
pkgver=2.2.3
pkgrel=5
pkgdesc="Automated Password Generator."
arch=(i686 x86_64)
url="http://www.adel.nursat.kz/apg/index.shtml"
license=('custom')
depends=('glibc')
changelog=$pkgname.changelog
source=(https://github.com/deogracia/orphaned-stuff/raw/master/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('acc1a1cca5f4a210f26fdf8c30704212a09bacc8cd1d4b294ddc129af9fd3e8ceb4dfebad70ba37fd09685df9009b8953b691fc014dc20c575544f71b08a81e3')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's:^#\(CS_LIBS = -lnsl\)$:\1:' Makefile
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  for i in apg apgbfm; do
    install -D -m755 $i $pkgdir/usr/bin/$i
    install -D -m644 doc/man/$i.1 $pkgdir/usr/share/man/man1/$i.1
  done
  install -D -m 644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
} 
