# Maintainer: Lionel Félicité <deogracia@free.fr>
# Contributor: Jakub Kozisek <nodevel at gmail dot com>

pkgname=python3-markups
_distname=pymarkups
pkgver=2.0.0
pkgrel=1
pkgdesc='Wrapper around various text markups'
url="https://github.com/retext-project/pymarkups"
depends=('python3')
optdepends=('python3-markdown: markdown support'
            'python3-docutils: reST support')
makedepends=('python3-setuptools')
license=('BSD')
arch=('any')
changelog=${pkgname}.changelog
source=(https://github.com/retext-project/${_distname}/archive/${pkgver}.tar.gz)
sha256sums=('0d1ac398ecde6ec3904ec842abee7538d58a3a2d51ac2e61584a822dc6d73a06')
_PYTHON_CMD="python3"

build() {
  cd "${srcdir}/${_distname}-${pkgver}"
  ${_PYTHON_CMD} setup.py build
}

check() {
  cd "${srcdir}/${_distname}-${pkgver}"
  pyvenv venv
  ./venv/bin/python3 setup.py develop test
}

package () {
  cd "${srcdir}/${_distname}-${pkgver}"
  ${_PYTHON_CMD} setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

