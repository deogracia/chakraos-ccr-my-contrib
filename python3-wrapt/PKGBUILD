# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=python3-wrapt
pkgver=1.10.11
pkgrel=1
pkgdesc="A Python module for decorators, wrappers and monkey patching"
arch=("i686" "x86_64")
url="https://pypi.python.org/pypi/wrapt"
license=("BSD")
makedepends=('python3' 'git')
depends=('python3')
checkdepends=('python3-pytest')
changelog=$pkgname.changelog
source=("git+https://github.com/GrahamDumpleton/wrapt.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
  cd "$srcdir"/wrapt
  python3 setup.py build
}

check() {
  cd "$srcdir"/wrapt
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.5:$PYTHONPATH" py.test
}

package() {
  cd wrapt
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
