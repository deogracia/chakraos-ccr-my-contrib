# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=python2-markdown
_distname='Python-Markdown'
pkgver=2.6.7
pkgrel=1
pkgdesc="Python implementation of John Gruber's Markdown."
arch=('any')
url='http://www.freewisdom.org/projects/python-markdown/'
license=('BSD')
depends=('python2')
checkdepends=('python2-nose')
changelog=${pkgname}.changelog
source=("https://github.com/waylan/${_distname}/archive/${pkgver}-final.tar.gz")
sha256sums=('31287f97286d365d7be056e2388904a13c29926abc275e3d31a3e4035c67122b')
_PYTHON_CMD="python2"

build() {
  cd ${_distname}-${pkgver}-final
  ${_PYTHON_CMD} setup.py build
}

check() {
  cd ${_distname}-${pkgver}-final
  nosetests2
}

package() {
  cd ${_distname}-${pkgver}-final
  ${_PYTHON_CMD} setup.py install --root="${pkgdir}" --optimize=1
  mv "${pkgdir}/usr/bin/markdown_py" "${pkgdir}/usr/bin/markdown_py2"

  install -D -m644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
