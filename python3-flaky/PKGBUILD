# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python3-flaky'
_distname='flaky'
pkgver=3.3.0
pkgrel=1
pkgdesc='Plugin for nose or py.test that automatically reruns flaky tests'
arch=('any')
license=('Apache')
url='https://github.com/box/flaky'
depends=('python3')
makedepends=('python3-setuptools' 'git')
checkdepends=('python3-pytest' 'python3-nose' 
              'python3-genty' 'python3-mock')
source=("https://github.com/box/${_distname}/archive/v${pkgver}.tar.gz")
changelog=${pkgname}.changelog
sha256sums=('822f445b4e0133541789b4281812c7592db6086db6b51e9321e8cc6efd2bc6eb')
_PYTHON_CMD="python3"

build() {
  cd "${srcdir}/${_distname}-${pkgver}"
  ${_PYTHON_CMD} setup.py build
}

check() {
  # Hack entry points by installing it

  cd "${srcdir}/${_distname}-${pkgver}"
  ${_PYTHON_CMD} setup.py install --root="${PWD}/tmp_install" --optimize=1
  (
    export PYTHONPATH="${PWD}/tmp_install/usr/lib/python3.5/site-packages:$PYTHONPATH"
    nosetests3 --with-flaky --exclude="test_nose_options_example" test/test_nose/
    py.test -k 'example and not options' --doctest-modules test/test_pytest/
    py.test -p no:flaky test/test_pytest/test_flaky_pytest_plugin.py
    nosetests3 --with-flaky --force-flaky --max-runs 2 test/test_nose/test_nose_options_example.py
    py.test --force-flaky --max-runs 2 test/test_pytest/test_pytest_options_example.py
  )
}

package() {
  cd "${srcdir}/${_distname}-${pkgver}"
  ${_PYTHON_CMD} setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
