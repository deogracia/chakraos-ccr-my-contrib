# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=perl-xml-sax-writer
_distname="XML-SAX-Writer"
pkgver=0.57
pkgrel=1
pkgdesc="XML-SAX-Writer perl module (SAX2 writer)"
arch=("any")
url="https://metacpan.org/release/XML-SAX-Writer"
license=("PerlArtistic")
depends=('perl' 'perl-text-iconv' 'perl-xml-filter-buffertext')
options=(!emptydirs)
changelog=${pkgname}.changelog
source=(https://cpan.metacpan.org/authors/id/P/PE/PERIGRIN/${_distname}-${pkgver}.tar.gz)
sha512sums=('8a6e8befdd91278b6e1dbce8ef62a630675f43e2debfb5833809fc46707d449c779d792e7aff44089f0509f9c3f8f10d576570ab0b8255f9f793802be92ad94f')

build() {
  cd $srcdir/${_distname}-${pkgver}
  # Install module into the vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
  make test
}

package() {
  cd $srcdir/${_distname}-${pkgver}
  make DESTDIR=$pkgdir install

  # Remove .packlist and perllocal.pod files.
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name 'perllocal.pod' -delete
}
