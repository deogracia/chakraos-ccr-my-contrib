# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python3-pyflakes'
_distname='pyflakes'
pkgver=1.3.0
pkgrel=2
pkgdesc="A simple program which checks Python source files for errors."
arch=('any')
url="https://github.com/PyCQA/pyflakes"
license=('MIT')
depends=('python3' 'python3-setuptools')
makedepends=('python3-setuptools')
checkdepends=('python3-setuptools')
changelog=${pkgname}.changelog
source=("https://github.com/PyCQA/${_distname}/archive/${pkgver}.tar.gz")
sha256sums=('7370356f3e20b537e61dfbcaf1ce7bf60aa7147e9e3e639e6401b445acfa3228')
provides=("${_distname}")
replaces=("${_distname}")
conflicts=("${_distname}")
_PYTHON_CMD="python3"

build() {
  cd "${srcdir}/${_distname}-${pkgver}"
  ${_PYTHON_CMD} setup.py build
}

check() {
  cd "${srcdir}/${_distname}-${pkgver}"
  ${_PYTHON_CMD} setup.py test
}

package() {
  cd "${srcdir}/${_distname}-${pkgver}"

  ${_PYTHON_CMD} setup.py install --root="$pkgdir/" --optimize=1

  # No physical licence file in this repo; I use the one created by me @ build time
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
