# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=ruby-commander
_distname=commander
pkgver=4.4.3
pkgrel=1
pkgdesc="The complete solution for Ruby command-line executables"
arch=('any')
url="https://github.com/commander-rb/commander"
license=('MIT')
depends=('ruby' 'ruby-highline>=1.7.2')
makedepends=('rubygems')
options=('!emptydirs')
changelog=${pkgname}.changelog
source=(https://rubygems.org/downloads/${_distname}-${pkgver}.gem)
noextract=(${_distname}-${pkgver}.gem)
sha256sums=('aedf4af6fdf8f05489001bcd70af87d83afec6896a3a2dfd9b49ec02bc391d07')

package() {
  cd "${srcdir}"

  local _rvm=$(env |grep rvm|wc -l)

  if [[ ${_rvm} -ne 0 ]] ; then
    . ~/.profile
    rvm use system
  fi

  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" "${_distname}-${pkgver}.gem"

  install -Dm644 "${pkgdir}/${_gemdir}/gems/${_distname}-${pkgver}/"LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
