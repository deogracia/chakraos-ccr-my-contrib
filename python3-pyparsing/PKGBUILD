# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python3-pyparsing'
pkgver=2.2.0
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='http://pyparsing.wikispaces.com/'
depends=('python3')
makedepends=('python3-setuptools' 'subversion')
license=('MIT')
changelog=$pkgname.changelog
source=("svn://svn.code.sf.net/p/pyparsing/code/tags/pyparsing_$pkgver")
sha256sums=('SKIP')

build() {
  cd "$srcdir"/pyparsing_$pkgver/src
  python3 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/pyparsing_$pkgver/src
  python3 unitTests.py
}

package() {

  cd pyparsing_$pkgver/src

  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
