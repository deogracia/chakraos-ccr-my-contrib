
pkgname=perl-test-harness
_distname=Test-Harness
pkgver=3.36
pkgrel=1
pkgdesc="Run Perl standard test scripts with statistics"
arch=('any')
url="https://metacpan.org/release/Test-Harness"
license=('GPL' 'PerlArtistic')
depends=('perl-extutils-makemaker')
makedepends=('perl-extutils-makemaker')
options=('!emptydirs')
changelog=$pkgname.changelog
source=("https://cpan.metacpan.org/authors/id/L/LE/LEONT/$_distname-$pkgver.tar.gz")
sha512sums=('77815ac1a5fda1b8089b2efafeca956087154ec30e9b1494333d55570709e0efcffe3d7281b7716d655eb692ff5aedf81e3acf6d13f1b9f58e728a0c63a0e99a')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distname-$pkgver"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$_distname-$pkgver"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$_distname-$pkgver"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
