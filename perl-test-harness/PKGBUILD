# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=perl-test-harness
_distname=Test-Harness
pkgver=3.39
pkgrel=1
pkgdesc="Run Perl standard test scripts with statistics"
arch=('any')
url="https://metacpan.org/release/Test-Harness"
license=('GPL' 'PerlArtistic')
depends=('perl-extutils-makemaker')
makedepends=('perl-extutils-makemaker')
options=('!emptydirs')
changelog=$pkgname.changelog
source=("https://cpan.metacpan.org/authors/id/L/LE/LEONT/$_distname-$pkgver.tar.gz")
sha512sums=('5465e0ce0f96da5656d8f45bbb490a7dab356f862093851e85372af1dd8ac4bcd074666efd9887582e71bd95e2340490589f561d8518a1bf8a312d156c020266')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distname-$pkgver"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$_distname-$pkgver"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$_distname-$pkgver"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
