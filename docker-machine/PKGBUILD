# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=docker-machine
_pkgname="machine"
pkgver=0.8.1
pkgrel=2
pkgdesc='Machine management for a container-centric world'
arch=('i686' 'x86_64')
url='https://github.com/docker/machine'
license=('Apache')
optdepends=('net-tools: for VirtualBox support')
makedepends=('docker')
changelog=$pkgname.changelog
source=("https://github.com/docker/machine/archive/v$pkgver.tar.gz")
sha256sums=('a16f30cf44e40457443f7bccc78c14c943770c4cfd7e227242c91eb8c4e95169')

build() {
  cd "${srcdir}"/${_pkgname}-${pkgver}
  export USE_CONTAINER=true
  make build
}

package() {
  cd "${srcdir}"/${_pkgname}-${pkgver}
  
  install -Dm755 bin/docker-machine "$pkgdir"/usr/bin/docker-machine
  install -Dm644 contrib/completion/bash/docker-machine.bash "$pkgdir"/usr/share/bash-completion/completions/docker-machine
  install -Dm644 contrib/completion/zsh/_docker-machine "$pkgdir"/usr/share/zsh/site-functions/_docker-machine
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
