# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python3-execnet'
pkgver=1.4.1
pkgrel=1
pkgdesc="Rapid multi-Python deployment"
arch=('any')
license=('MIT')
url="http://codespeak.net/execnet"
makedepends=('python3-setuptools' 'python3-apipkg' 'python3-setuptools-scm')
checkdepends=('python3-pytest-timeout' 'procps-ng' 'openssh' 'pypy')
depends=('python3-apipkg')
changelog=$pkgname.changelog
source=("http://pypi.python.org/packages/source/e/execnet/execnet-$pkgver.tar.gz")
sha512sums=('e8c0c32e873f8585ff4d4da3b9cbd40046479370f5653f67a98c9029d4535a619731d1b825332fe4af4dc5ebc9ab11ee879ef45df07f08943b1e1685f2255790')

check() {
  cd "$srcdir/execnet-${pkgver}"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test testing
}

build() {
  cd "$srcdir/execnet-${pkgver}"
#  grep -rl -e "\#\!\ \/usr\/bin\/env\ python" . |xargs sed -i "s|#! /usr/bin/env python$|#!/usr/bin/env python3|"
  find . -type f -name '*.py' -exec sed -i 's|#! /usr/bin/env python$|#!/usr/bin/env python3|' {} \;
  python3 setup.py build
}

package() {
  cd execnet-${pkgver}
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
