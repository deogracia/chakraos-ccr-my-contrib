# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=python3-setuptools-scm
pkgver=1.15.5
pkgrel=1
_commit='75897ba511010efaabf628217d34b589d9299c8f'
pkgdesc="Handles managing your python package versions in scm metadata."
arch=('any')
url="https://github.com/pypa/setuptools_scm"
license=('MIT')
depends=('python3-setuptools')
makedepends=('python3-setuptools' 'python3-pip' 'git')
checkdepends=('python3-pytest-runner' 'mercurial')
changelog=$pkgname.changelog
source=("git+https://github.com/pypa/setuptools_scm.git#commit=$_commit")
md5sums=('SKIP')

build() {
  cd "$srcdir"/setuptools_scm
  python3 setup.py build
  python3 setup.py egg_info
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/setuptools_scm
  python3 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.5/site-packages:$PYTHONPATH" python3 setup.py ptr
}

package() {

  cd "$srcdir"/setuptools_scm
  python3 setup.py install --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
