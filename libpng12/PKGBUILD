# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=libpng12
_realname=libpng
pkgver=1.2.59
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
changelog=${pkgname}.changelog
source=("libpng-${pkgver}.tar.xz::https://sourceforge.net/projects/libpng/files/libpng12/${pkgver}/libpng-${pkgver}.tar.xz/download"
        "libpng-${pkgver}-apng.patch.gz::https://sourceforge.net/projects/libpng-apng/files/libpng12/${pkgver}/libpng-${pkgver}-apng.patch.gz/download")
sha256sums=('b4635f15b8adccc8ad0934eea485ef59cc4cae24d0f0300a9a941e51974ffcc7'
            '281fd5f0165762967a18302dca217de3212be4a3437f95805be44f1ac9db1a5d')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/libpng-${pkgver}-apng.patch"

  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include/*.h,lib/{libpng.{a,so},pkgconfig},share}

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/libpng12/LICENSE

}
