# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python3-pluggy'
_distname=pluggy
pkgver=0.5.2
pkgrel=1
pkgdesc="Plugin and hook calling mechanisms for python"
arch=('any')
url="https://www.pluggypayments.com/docs/python"
license=('MIT')
makedepends=('python3-setuptools')
checkdepends=('python3-pytest-runner')
depends=('python3')
changelog=${pkgname}.changelog
source=("https://pypi.python.org/packages/c1/c8/d0c5ca3c8134cbc7c8e2a40a0f908b3aa0e76762b3a829ae6dbe26c1f2b2/${_distname}-${pkgver}.tar.gz#md5=7b26ef8148c330ab17796b4f297527c0")
sha512sums=('850b1a7b9e55d46d5deb16b8bc71e3f9d6c26b78fe34379b295a9780f3da8abb3c44f524f175d0392b61fc2d36d66d156db3f499a3c1666e2ff3e3c6344153cb')

build() {
  cd "${srcdir}"/${_distname}-${pkgver}
  python3 setup.py build
}

check() {
  cd "${srcdir}"/${_distname}-${pkgver}
  python3 setup.py ptr
}

package() {
  cd "${srcdir}"/${_distname}-${pkgver}
  python3 setup.py install -O1 --root "${pkgdir}"

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
