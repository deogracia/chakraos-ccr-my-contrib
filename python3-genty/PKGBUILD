# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python3-genty'
_distname='genty'
pkgver=1.3.2
pkgrel=2
pkgdesc="Allows you to run a test with multiple data sets"
arch=('any')
license=('Apache')
url="https://github.com/box/genty"
depends=('python3-six')
makedepends=('python3-setuptools' 'python3-six' 'git')
checkdepends=('python3-mock')
changelog=${pkgname}.changelog
source=("git+https://github.com/box/${_distname}.git#tag=v$pkgver")
sha512sums=('SKIP')
_PYTHON_CMD="python3"

build() {
  cd "${srcdir}/${_distname}"
  ${_PYTHON_CMD} setup.py build
}

check() {
  cd "${srcdir}/${_distname}"
  ${_PYTHON_CMD} setup.py test
}

package() {
  cd "${srcdir}/${_distname}"
  ${_PYTHON_CMD} setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
