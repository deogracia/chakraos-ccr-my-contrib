# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgbase=python-markups
pkgname=(python3-markups python2-markups)
pkgver=1.0.1
pkgrel=1
pkgdesc='Wrapper around various text markups'
arch=('any')
url='https://launchpad.net/python-markups'
license=('BSD')
makedepends=('python3-setuptools' 'python2-setuptools' 'python2' 'python3')
changelog=$pkgbase.changelog
source=(http://pypi.python.org/packages/source/M/Markups/Markups-$pkgver.tar.gz)
sha256sums=('74464b7532aa355f6ed2767fd5cfb5eb5b484dd773b33bae4016a7fa9354b4ee')

package_python3-markups() {
  depends=('python3')
  optdepends=('python3-markdown: markdown support'
              'python3-docutils: reST support')

  cd "${srcdir}"/Markups-$pkgver
  python3 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-markups() {
  depends=('python2')
  optdepends=('python2-markdown: markdown support'
              'python2-docutils: reST support')

  cd "${srcdir}"/Markups-$pkgver
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
