# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=perl-devel-stacktrace
_distname=Devel-StackTrace
pkgver=2.00
pkgrel=1
pkgdesc='An object representing a stack trace'
arch=('any')
url="https://metacpan.org/release/Devel-StackTrace"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.6')
options=('!emptydirs')
changelog=$pkgname.changelog
source=("http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/$_distname-$pkgver.tar.gz")
sha256sums=('1debe7273099a60e1386e0da5edbed7334db3cf3ed8e3b4106b087100b8ec5e4')

build() {
  cd "${srcdir}"/$_distname-$pkgver

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  perl Makefile.PL
  make
}

check() {
  cd "${srcdir}"/$_distname-$pkgver

  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "${srcdir}"/$_distname-$pkgver

  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
