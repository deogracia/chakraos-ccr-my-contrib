# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=perl-devel-stacktrace
_distname=Devel-StackTrace
pkgver=2.02
pkgrel=1
pkgdesc='An object representing a stack trace'
arch=('any')
url="https://metacpan.org/release/Devel-StackTrace"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.6')
options=('!emptydirs')
changelog=${pkgname}.changelog
source=("http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/${_distname}-${pkgver}.tar.gz")
sha256sums=('cbbd96db0ecf194ed140198090eaea0e327d9a378a4aa15f9a34b3138a91931f')

build() {
  cd "${srcdir}"/${_distname}-${pkgver}

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  perl Makefile.PL
  make
}

check() {
  cd "${srcdir}"/${_distname}-${pkgver}

  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "${srcdir}"/${_distname}-${pkgver}

  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
