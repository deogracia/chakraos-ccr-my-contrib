# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='python3-pytest-timeout'
pkgver=1.0.0
pkgrel=1
pkgdesc='py.test plugin to abort hanging tests'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-timeout'
makedepends=('python3-pytest' 'mercurial')
checkdepends=('python3-pexpect')
depends=('python3-pytest')
changelog=$pkgname.changelog
source=("hg+https://bitbucket.org/pytest-dev/pytest-timeout#tag=$pkgver")
md5sums=('SKIP')

build() {
  cd "$srcdir"/pytest-timeout
  python3 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-timeout
  python3 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.5/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-timeout
  python3 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
# vim:set ts=2 sw=2 et:
