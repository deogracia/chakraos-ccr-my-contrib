# Maintainer: Lionel Félicité <deogracia@free.fr>
# Contributor: Sébastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Manolis Tzanidakis

pkgname=makepasswd
pkgver=1.10_10
pkgrel=2
pkgdesc='Generates true random passwords with the emphasis on security over pronounceability (Debian version)'
arch=('any')
url='http://packages.qa.debian.org/m/makepasswd.html'
license=('GPL2')
depends=('perl-passwd-md5' 'perl-crypt-openssl-random' 'perl-bytes-random-secure')
changelog=$pkgname.changelog
source=("http://ftp.debian.org/debian/pool/main/m/${pkgname}/${pkgname}_${pkgver%_*}.orig.tar.gz"
        "http://ftp.debian.org/debian/pool/main/m/${pkgname}/${pkgname}_${pkgver//_/-}.diff.gz")
md5sums=('55842bb4c064ebcdedffb4afd5f0945a'
         'e9ec121ed24c08f7b9b21b325f3a9fef')

package() {
  cd $pkgname-${pkgver%_*}
  patch -p 1 -i "$srcdir/${pkgname}_${pkgver//_/-}.diff"
  install -D -m0755 makepasswd "$pkgdir/usr/bin/makepasswd"
  install -D -m0644 makepasswd.1 "$pkgdir/usr/share/man/man1/makepasswd.1"
}

# vim:set ts=2 sw=2 ft=sh et: 
