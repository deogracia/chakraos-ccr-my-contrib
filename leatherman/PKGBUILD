# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=leatherman
pkgver=1.0.0
pkgrel=1
pkgdesc="Collection of C++ and CMake utility libraries"
arch=('i686' 'x86_64')
url="https://github.com/puppetlabs/leatherman"
license=('APACHE')
makedepends=('boost' 'cmake' 'rapidjson')
checkdepends=('ruby')
changelog=${pkgname}.changelog
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/puppetlabs/leatherman/archive/${pkgver}.tar.gz)
sha256sums=('4bcb8eaf08429e9e62cc6fcbfc1f216bd2eff800f751dbacabbec7d714ed21a2')

prepare() {
  cd ${pkgname}-${pkgver}

  # Ruby 2.3 fix: replace rb_data_object_alloc symbol with rb_data_object_wrap
  sed -i 's/rb_data_object_alloc/rb_data_object_wrap/g' \
    $( grep -rl rb_data_object_alloc ruby )
}

build() {
  cd ${pkgname}-${pkgver}

  cmake -DCMAKE_INSTALL_PREFIX=/usr .

  make
}

check() {
  cd ${pkgname}-${pkgver}

  LANG=C make test
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR="${pkgdir}"

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
