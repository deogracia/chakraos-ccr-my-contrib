# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname='jenkins'
pkgver='2.73.3'
pkgrel=1
pkgdesc="Build great things at any scale. LTS version"
arch=('any')
url="https://jenkins.io/"
license=('MIT')
depends=('java-runtime' 'libcups' 'ttf-dejavu')
optdepends=()
provides=("${pkgname}")
backup=('etc/conf.d/jenkins')
install=${pkgname}.install
changelog=${pkgname}.changelog
source=("${pkgname}-${pkgver}.war::http://mirrors.jenkins.io/war-stable/${pkgver}/${pkgname}.war"
         #Rename to jenkins-${pkgver}.war to force download on new version
        "https://github.com/jenkinsci/jenkins/raw/master/LICENSE.txt"
        "${pkgname}.conf"
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
noextract=("${pkgname}-${pkgver}.war")
sha256sums=('fcfb932a84ce03795abbf51ae7799439278155b0d42242d1d79a187034649fc8'
            '0599d996b7b64a61ff60fabb4b69dc807b6d396abbc6d2ddb43c3dc36204b89d'
            '13b6e06de1dedff96bdb8e43f6830bbd954dc58df9d4ed5583693d2a6f8427f4'
            '01b84f584a1bfea3ad5411a380ca6954aed2d47fa4a08b92b23b87c5d31e5e1d'
            '13f81f3aa069114f4b24a7ff045d8aa142df9a114b6038890fd5c16e2f39f686'
            '41a016deda65f04ab4eaa40e4becd83660e8374b68fa0bb114f842927c108350')

package() {
  install -Dm444 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
  install -Dm444 "${pkgname}-${pkgver}.war" "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.war"
  install -Dm644 "${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -Dm644 "${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm644 "${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm644 "${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
}
