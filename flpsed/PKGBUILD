# Maintainer: Lionel Félicité <deogracia@free.fr>
# Contributor: Ivy Foster <joyfulgirl (at) archlinux.us>
# Contributor: aur2ccr (http://ddg.gg/?q=!ccr+aur2ccr&t=chakra)
# Contributor: Giorgio Lando <patroclo7@gmail.com>

pkgname=flpsed
pkgver=0.7.3
pkgrel=2
pkgdesc="A WYSIWYG PostScript annotator"
arch=('i686' 'x86_64')
url="http://flpsed.org/flpsed.html"
license=("GPL")
depends=('ghostscript' 'fltk')
changelog=$pkgname.changelog
install=$pkgname.install
source=(http://flpsed.org/${pkgname}-${pkgver}.tar.gz
        flpsed.desktop)
sha1sums=('460bc23a63c32a9b9241f501f23d85ca7cda2834'
          '8f89774c17cf1ee5e45541685a06e118c24f85fd')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=$pkgdir install
  install -D -m644 ../flpsed.desktop ${pkgdir}/usr/share/applications/flpsed.desktop
}

# End of file
