# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=puppet-lint
pkgver=2.1.0
pkgrel=1
pkgdesc="Check that your Puppet manifests conform to the style guide."
arch=('any')
url="https://github.com/rodjek/puppet-lint"
license=('MIT')
depends=('ruby')
makedepends=('rubygems' 'help2man')
changelog=${pkgname}.changelog
options=('!emptydirs')
source=(https://rubygems.org/downloads/${pkgname}-${pkgver}.gem)
noextract=(${pkgname}-${pkgver}.gem)
sha256sums=('9e22529314ed737b4cde3f1e1f221e044661534e85149e70abf0546d1f633be1')

package() {
  cd "${srcdir}"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" "${pkgname}-${pkgver}.gem"

  # Generate man page
  GEM_PATH="${pkgdir}${_gemdir}" help2man --no-info --output "${pkgname}.1" "${pkgdir}/usr/bin/puppet-lint"

  install -Dm644 "${pkgdir}${_gemdir}/gems/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/${pkgname}.1"                                "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
