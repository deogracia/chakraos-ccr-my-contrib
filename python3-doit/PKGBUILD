# Maintainer: Lionel Félicité <deogracia@free.fr>

pkgname=python3-doit
_pyname=doit
pkgver=0.30.3
pkgrel=1
pkgdesc='doit automation tool'
arch=('any')
url='http://pydoit.org/'
license=('MIT')
options=(!emptydirs)
changelog=${pkgname}.changelog
source=("https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
depends=('python3' 'python3-pyinotify' 'python3-six' 'python3-setuptools' 'python3-cloudpickle')
sha256sums=('2988c8450fb3fb281877eb9a16860e1a8873c6ee0bb75ef0d78bb03a18d59ab9')

package() {
  cd "${srcdir}/${_pyname}-${pkgver}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
  ln -s ${_pyname} "${pkgdir}/usr/bin/${_pyname}3"
  install -D -m755 'zsh_completion_doit' "${pkgdir}/usr/share/zsh/site-functions/_${_pyname}"
  install -D -m755 'bash_completion_doit' "${pkgdir}/usr/share/bash-completion/completions/${_pyname}"
}

# vim:set ts=2 sw=2 et:
